@name Countdown
@inputs 
@outputs Days Hours Minutes Seconds Milliseconds Done
@persist TargetTime PlusTime Running
@trigger

interval(1)
runOnChat(1)
Authorized = 0

LastSpoke = lastSpoke()
if (chatClk() & LastSpoke == owner()) {
    LastSaid = lastSaid()
    Authorized = 1
}
if (Authorized) {
    # Parse the command
    if (LastSaid:find(" ")) {
        Command = LastSaid:sub(1, LastSaid:find(" ") - 1)
        Params = LastSaid:sub(LastSaid:find(" ") + 1, LastSaid:length()):trimLeft():trimRight()
    } else {
        Command = LastSaid:sub(1, LastSaid:length())
        Params = ""
    }
    Components = Params:explode(" ")
    RComponents = array()
    for (I = 1, Components:count()) {
        RComponents:pushString(Components:string(Components:count() - I + 1))
    }
    IsCmd = 0
    if (Command:index(1) == "!" | Command:index(1) == "/") {
        IsCmd = 1
        Command = Command:sub(2, Command:length()):lower()
    }
    if (IsCmd) {
        hideChat(1)
        if (Command == "countdown") {
            Running = 0
            PlusTime = RComponents:number(1) + RComponents:number(2) * 60 + RComponents:number(3) * 60 * 60 + RComponents:number(4) * 60 * 60 * 24
        } elseif (Command == "start") {
            TargetTime = curtime() + PlusTime
            Running = 1
            hint("Started countdown.", 10)
        }
    }
} else {    
    TimeDelta = TargetTime - curtime()
    if (!Running) {
        TimeDelta = PlusTime
    }
    Done = 0
    if (TimeDelta < 0) {
        Done = 1
        TimeDelta = 0
    }
    
    Milliseconds = floor(TimeDelta % 1 * 1000)
    Seconds = floor(TimeDelta % 60)
    Minutes = floor(TimeDelta / 60 % 60)
    Hours = floor(TimeDelta / 60 / 60 % 24)
    Days = floor(TimeDelta / 60 / 60 / 24)
}
